plugins {
    id 'java'
    id "org.sonarqube" version "4.4.1.3373"
    id 'jacoco'
}

jacocoTestReport {
    reports {
        xml.required = true
        dependsOn test // tests are required to run before generating the report
    }
}

sonar {
    properties {
        property "sonar.projectKey", "grupo60-fiap2023_snackhub"
        property "sonar.organization", "grupo60-fiap2023"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

group = 'com.snackhub.application'

repositories {
    mavenCentral()
}

dependencies {
    implementation(project(":domain"))
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacoco {
    toolVersion '0.8.10'
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

tasks.create(name: "testCoverage", type: JacocoReport, dependsOn: "test") {

    group = "Reporting"
    description = "Generate Jacoco coverage reports for the test build."

    reports {
        html.required.set(true)
        xml.required.set(true)
    }

    def excludes = [
            '**/*Test*.*',
            '**/actions/*.*',
            '**/core/*.*',
            '**/markers/*.*',
            '**/services/**/*.*',
            '**/toolwindow/*.*',
            '**/utils/*.*'
    ]

    def javaDomainClasses = fileTree(dir: "${buildDir}/domain/classes/java/main", excludes: excludes)
    def javaAppClasses = fileTree(dir: "${buildDir}/application/classes/java/main", excludes: excludes)
    classDirectories.from = files([javaAppClasses, javaDomainClasses])

    sourceDirectories.from = files([
            "$project.projectDir/domain/src/main/java",
            "$project.projectDir/application/src/main/java",
            "$buildDir/generated/source/kapt/test"
    ])

    executionData.from = files("${project.buildDir}/domain/test.exec")
    executionData.from = files("${project.buildDir}/application/test.exec")

}